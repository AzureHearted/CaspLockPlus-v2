#Requires AutoHotkey v2.0

/**
 * æ˜¾ç¤ºToolTipsæ¶ˆæ¯
 * @param {String} msg æ¶ˆæ¯å†…å®¹
 * @param {Integer} duration æŒç»­æ—¶é—´ms
 * @param {Integer} id å¦‚æœçœç•¥, é»˜è®¤ä¸º 1(ç¬¬ä¸€ä¸ªå·¥å…·æç¤º). å¦åˆ™, è¯·æŒ‡å®šä¸€ä¸ªä»‹äº 1 å’Œ 20 ä¹‹é—´çš„æ•°å­—, åœ¨åŒæ—¶ä½¿ç”¨äº†å¤šä¸ªå·¥å…·æç¤ºæ—¶, ç”¨æ¥è¡¨ç¤ºè¦æ“ä½œçš„å·¥å…·æç¤ºçª—å£.
 */
ShowToolTips(msg, duration := 1000, id := 1) {
    static channelMap := Map()  ; ç”¨æ¥ä¿å­˜æ¯ä¸ª id çš„å®šæ—¶å™¨

    ; --- å…ˆæ˜¾ç¤ºæ¶ˆæ¯ ---
    ToolTip(msg, , , id)

    ; --- å¦‚æœè¯¥ id å·²ç»å­˜åœ¨å®šæ—¶å™¨ï¼Œåˆ™å…ˆåˆ é™¤å®ƒï¼ˆé˜²æ­¢é‡å¤å®šæ—¶ï¼‰ ---
    if channelMap.Has(id) {
        SetTimer(channelMap[id], 0)
    }

    ; --- å®šä¹‰ä¸€ä¸ªæ–°çš„å®šæ—¶å™¨ï¼Œç”¨äºéšè—è¯¥ ToolTip ---
    timerFunc := (*) => (
        ToolTip(, , , id),  ; æ¸…é™¤å¯¹åº” id çš„ ToolTip
        channelMap.Delete(id)  ; ä» map ç§»é™¤ï¼ˆé‡Šæ”¾èµ„æºï¼‰
    )

    ; --- ä¿å­˜å¹¶å¯åŠ¨å®šæ—¶å™¨ ---
    channelMap[id] := timerFunc
    SetTimer(timerFunc, -duration)
}


/**
 * è·å–é€‰ä¸­çš„æ–‡æœ¬(æ”¯æŒæ— æ±¡æŸ“å‰ªè´´æ¿)
 * @param {Integer} endDelay è·å–å®Œæˆåå»¶æ—¶å¤šå°‘mså†è¿”å›ç»“æœ
 * @returns {String} è·å–åˆ°çš„æ–‡æœ¬å†…å®¹
 */
GetSelText(endDelay := 0) {
    ClipboardOld := ClipboardAll()
    A_Clipboard := ""
    SendInput('^{c}')
    if (ClipWait(0.05, 0)) {
        selText := A_Clipboard
        ; è¿˜åŸå‰ªè´´æ¿ğŸ“‹
        A_Clipboard := ClipboardOld

        lastChar := SubStr(selText, StrLen(selText), 1)
        if (Ord(lastChar) != 10) ;å¦‚æœæœ€åä¸€ä¸ªå­—ç¬¦æ˜¯æ¢è¡Œç¬¦ï¼Œå°±è®¤ä¸ºæ˜¯åœ¨IDEé‚£å¤åˆ¶äº†æ•´è¡Œï¼Œä¸è¦è¿™ä¸ªç»“æœ
        {
            Console.Debug('è·å–æ–‡æœ¬æˆåŠŸ:' . selText . '`n')
            Sleep(endDelay)
            return selText
        } else {
            Console.Debug('æœªé€‰ä¸­æ–‡æœ¬:' . '`n')
            Sleep(endDelay)
            return
        }
    } else {
        Console.Debug("å‰ªè´´æ¿ç­‰å¾…è¶…æ—¶")
        ; è¿˜åŸå‰ªè´´æ¿ğŸ“‹
        A_Clipboard := ClipboardOld
        Sleep(endDelay)
        return ""
    }
}


;! è·å–æ´»åŠ¨çš„èµ„æºç®¡ç†å™¨è·¯å¾„
GetActiveExplorerPath() {
    hwndActive := WinActive("A")
    shellApp := ComObject("Shell.Application")
    try {
        for (window in shellApp.Windows) {
            if InStr(window.FullName, "explorer.exe") && window.HWND = hwndActive {
                return window.Document.Folder.Self.Path
            }
        }
    } catch as ex {
        Console.Debug("GetActiveExplorerPath æ‰§è¡Œå‡ºé”™ï¼š" ex.Message)
    }
    return ""
}

;! è·å–é€‰ä¸­çš„é¡¹(æ–‡ä»¶èµ„æºç®¡ç†å™¨ä¸­)çš„è·¯å¾„åˆ—è¡¨
GetSelectedExplorerItemsPaths() {
    hwndActive := WinActive("A") ; è·å–å½“å‰æ´»åŠ¨çª—å£å¥æŸ„
    shellApp := ComObject("Shell.Application")
    paths := []

    try {
        for (window in shellApp.Windows) {
            ; åªå¤„ç† explorer.exe ç›¸å…³çª—å£
            if InStr(window.FullName, "explorer.exe") {
                ; å¯¹æ¯”çª—å£å¥æŸ„
                if (window.HWND = hwndActive) {
                    for (item in window.Document.SelectedItems) {
                        ; Console.Debug(item.Path)
                        paths.Push(item.Path)
                    }
                    return paths
                }
            }

        }
    } catch as ex {
        Console.Debug("GetSelectedExplorerItemsPaths æ‰§è¡Œå‡ºé”™ï¼š" ex.Message)
    }
    return paths
}

/**
 * ! å±…ä¸­æ˜¾ç¤ºçª—å£
 * @param {String} WinTitle 'ahk_exe '|'ahk_class '|'ahk_id '|'ahk_pid '|'ahk_group '
 */
CenterWindow(WinTitle := 'A') {
    ; è·å–çª—å£ä½ç½®å’Œå¤§å°
    if (!WinExist(WinTitle))
        return

    WinGetPos(&x, &y, &w, &h, WinTitle)
    ; è®¡ç®—å±å¹•ä¸­å¿ƒä½ç½®
    cx := (A_ScreenWidth - w) / 2
    cy := (A_ScreenHeight - y) / 2
    ; ç§»åŠ¨çª—å£
    return WinMove(cx, cy, , , WinTitle)
}

/**
 * ! åˆ¤æ–­ä¸€ä¸ªå­—ç¬¦ä¸²ä¸­æ˜¯å¦åŒ…å«æ•°ç»„ä¸­çš„ä»»æ„ä¸€é¡¹
 * @param {String} targetStr æµ‹è¯•å­—ç¬¦ä¸²
 * @param {Array} patterns å­—ç¬¦ä¸²æ•°ç»„
 * @param {Integer} CaseSense æ˜¯å¦åŒºåˆ†å¤§å°å†™ (é»˜è®¤ä¸åŒºåˆ†å¤§å°å†™)
 * @returns {Integer} æµ‹è¯•ç»“æœ
 */
StrIncludesAny(targetStr, patterns, CaseSense := 0) {
    for (item in patterns) {
        if InStr(StrLower(targetStr), StrLower(item), CaseSense)
            return true
    }
    return false
}


/**
 * ! åˆ¤æ–­çª—å£æ˜¯å¦ç½®é¡¶
 * @param {Integer} hwnd çª—å£id
 */
IsAlwaysOnTop(hwnd := 0) {
    try {
        exStyle := WinGetExStyle(hwnd > 0 ? ('ahk_id ' hwnd) : 'A')   ;è·å–æ‰©å±•æ ·å¼
        return exStyle & 0x8
    } catch as e {
        Console.Debug('IsAlwaysOnTopé”™è¯¯æ¶ˆæ¯:' e.Message)
        return false
    }
}